import os

SetOption('num_jobs', 8)

CCFLAGS = ("-m32"
           " -static"
           " -nostdlib"
           " -nostartfiles"
           " -ffreestanding"
           " -Wa,-march=RVIMAFDXhwacha"
)

CC = '/opt/riscv/bin/riscv64-unknown-elf-gcc'

riscv_program_env = Environment(
    CC = CC,
    AS = CC,
    LD = CC,
    CCFLAGS = CCFLAGS,
    ASFLAGS = CCFLAGS,
    LINKFLAGS = CCFLAGS
)
Export('riscv_program_env')


CHISEL_JAR     = "/usr/share/java/chisel/chisel.jar"
COMMONS_IO_JAR = "/usr/share/java/commons-io/commons-io.jar"
CLASSPATH = "{0}:{1}".format(CHISEL_JAR, COMMONS_IO_JAR)
Export('CLASSPATH')

riscv_tests     = SConscript('riscv_tests/SConscript', variant_dir='build/riscv_tests' , duplicate=0)
turborav_jar    = SConscript('src/SConscript')
default_program = SConscript('src/programs/SConscript', variant_dir='build/programs' , duplicate=0)

# Verilog code generation of build/verilog/Soc.v
env = Environment(ENV = os.environ)
env.Command(
    "build/verilog/Soc.v",
    [turborav_jar, default_program],
    'scala -classpath build/scala/classes:{0} TurboRav.TurboRav Soc {1} {2} 12 8'
    .format(
        CLASSPATH,
        "build/verilog/".format(default_program),
        default_program
    )
)

# Riscv-tests
for test in riscv_tests:
    test_path = str(test)
    test_name = test_path.split('/')[-1]
    target_dir = "build/test/riscv/{0}/".format(test_name)
    env.Command(
        target_dir + "jenkins.xml",
        [turborav_jar, test],
        'CXX=clang++ scala -classpath build/scala/classes:{0} TurboRav.TurboRavTestRunner riscvtest {1} {2} 12 8'
        .format(
            CLASSPATH,
            target_dir,
            test
        )
    )

# Unit tests, or Chisel based testbenches.
# NB: Duplicated in turboravtester.scala
test_benches = [
    "alu",
    "bru",
    "fwu",
    "regbank",
    "mult",
    "timer",
    "decode",
    "execute",
    "memory",
    "writeback",
    "ravv",
    "soc"
]
for module in test_benches:
    target_dir = "build/test/unit/{0}/".format(module)
    env.Command(
        target_dir + "jenkins.xml",
        [turborav_jar, default_program],
        'CXX=clang++ scala -classpath build/scala/classes:{0} TurboRav.TurboRavTestRunner {1} {2} {3} 12 8 || true'
        .format(
            CLASSPATH,
            module + "test",
            target_dir,
            default_program
        )
    )
