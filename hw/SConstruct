import os
import subprocess
import multiprocessing

Help("""Type:
'scons build/test'                             run all tests,
'scons build/test/riscv/'                      run all riscv tests,
'scons build/test/riscv/add'                   run the riscv add test,
'scons build/test/unit/'                       run all unit tests,
'scons build/test/unit/Alu'                    run the Alu test from src/test/scala,
'scons build/test/lint'                        lint the scala code,
'scons build/synthesis/Soc.v'                  generate verilog code,
'scons build/synthesis/xilinx/turborav.bit'    generate a netlist,
'scons build/synthesis/xilinx/programming.log' program a netlist,
'scons build'                                  build everything.

All build targets are files, or directories of files that can
be "built" by the build system. Even programming a netlist is
considered just a build target that generates a programming.log
text file.
""")

# Multi-threaded builds
SetOption("num_jobs", multiprocessing.cpu_count() + 1)

# Use host environment by default.
DefaultEnvironment(ENV = os.environ)

# This ensures the entire build directory is cleaned, even files in
# there that SCons didn't know that it generated.
Clean("build", "build")
if GetOption("clean"):
    # If you change a library like chisel.jar you need to also
    # invalidate the fsc cache. This can be hard to remember so we
    # invalidate the fsc cache on every clean command.
    subprocess.call(["fsc", "-reset"])

SConscript("src/SConscript", variant_dir = "build")
