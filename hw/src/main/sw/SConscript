import os

Import('env')

COMMON_FLAGS = (
                " -O3 "
                " -Os "
)

CCFLAGS = COMMON_FLAGS + \
          (" -static"
           " -c"
           " -m32"
           " -Wa,-march=RVIMAFDXhwacha "
           " --std=gnu11 "
           " -save-temps=obj "
)

LINKER_FLAGS = (" -T$LINKER_SCRIPT "
                " -m elf32lriscv "
                " -Map=${TARGET}.map "
)

CC = 'riscv64-unknown-elf-gcc'
LD = 'riscv64-unknown-elf-ld'

linker_script = SConscript("linker_script/SConscript")

riscv_program_env = env.Clone(
    CC = CC,
    AS = CC,
    LINK = LD,
    LIBS = File("/opt/riscv/riscv64-unknown-elf/lib/soft-float/32/no-atomic/libc.a"),
    CCFLAGS = CCFLAGS,
    ASFLAGS = CCFLAGS,
    LINKFLAGS = COMMON_FLAGS + LINKER_FLAGS,
    LINKER_SCRIPT = linker_script,
)
riscv_program_env.Append(
    BUILDERS = {
        'Disassemble' : Builder(
            action = """riscv64-unknown-elf-objdump \
            -D -M no-aliases -M numeric $SOURCE > $TARGET""",
            suffix = ".dis"
        )
    }
)

SConscript(
    "crt0/SConscript",
    exports = "riscv_program_env"
)

default_program = SConscript(
    'programs/SConscript',
    exports = "riscv_program_env"
)

sw_env = SConscript(
    "system_software/SConscript",
    exports = "riscv_program_env"
)

sw_env['DEFAULT_PROGRAM'] = default_program

Return('sw_env')
