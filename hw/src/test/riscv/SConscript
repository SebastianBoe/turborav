from subprocess import check_output

Import('scala_env turborav_jar CLASSPATH riscv_program_env startup_file')

# Here we build the riscv tests and also run the tests to generate
# jenkins.xml reports.

# Directory of riscv tests. (Just the Riscv ISA version we are
# supporting.)
RV32UI_PATH = "riscv-tools/riscv-tests/isa/rv32ui/"

black_list_names = [
    'amoswap_w',
    'amomaxu_w',
    'amominu_w',
    'amoadd_w',
    'amoand_w',
    'amomax_w',
    'amomin_w',
    'amoor_w',
    'fence_i',
    'divuw',
    'lrsc',
    'divw',
    'mulw',
    'lwu',
    'ld'
]

black_list = [
    File("{0}{1}.S".format(RV32UI_PATH, name))
    for name in black_list_names
]

test_sources = set(Glob(RV32UI_PATH + "/*.S")) - set(black_list)

CPPPATH = [
    'riscv-tools/riscv-tests/isa/macros/scalar/',
    'riscv-tools/riscv-tests/env/p'
]

for test in test_sources:
    program = riscv_program_env.Program(
        [test, startup_file],
        CPPPATH = CPPPATH
    )[0]
    Depends(program, "#build/misc/turborav.ld")
    test_name = str(program).split('/')[-1]

    target_dir = Dir(test_name).path
    scala_env.Command(
        test_name + "/jenkins.xml",
        [turborav_jar, program],
        """CXX=clang++ scala \
        -classpath {3}:{0}\
        TurboRav.TurboRavTestRunner Riscvtest {1} {2} 12 8"""
        .format(
            CLASSPATH,
            target_dir,
            program.path,
            turborav_jar
        )
    )
