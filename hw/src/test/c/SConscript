Import('riscv_program_env startup_file turborav_jar sim_env')

# C-tests re-use the same toplevel scala simulation testbench
# (src/test/scala/Riscvtest.scala) that is used for simulating RISC-V
# assembly tests (src/test/riscv/).

# This testbench controls a simulation of software running on the SoC
# and looks for special signals that indicate test pass, test fail, or
# printf messages.
module = "Riscvtest"

c_sim_env = sim_env.Clone(
    MODULE     = module,
    TOP_MODULE = "Soc"
)

c_test_env = riscv_program_env.Clone()
c_test_env.Append(CPPPATH = ["modules"])

for test_suite in [test_suite for test_suite in Glob("modules/*") if test_suite.isdir()]:
    test_suite_lib_dir = test_suite.Dir(".lib")
    c_test_env.Append(CPPPATH = test_suite_lib_dir)
    for test in test_suite.glob("*.c"):
        program = c_test_env.Program(
            [
                test,
                "modules/tr.c",
                "modules/unit.c",
                test_suite_lib_dir.glob("*.c"),
                "syscalls.c",
                startup_file
            ]
        )[0]
        Depends(program, c_test_env["LINKER_SCRIPT"])

        target_dir = str(program.path) + "_dir"
        c_sim_env.Simulate(
            "#" + target_dir + "/jenkins.xml",
            [turborav_jar, program],
            TARGET_DIR = target_dir,
            ROM = program.path
        )
        # Generate disassembly for debugging
        riscv_program_env.Command(
            str(program) + ".dis",
            program,
            """riscv64-unknown-elf-objdump \
            -DS -M no-aliases -M numeric \
            $SOURCE > $TARGET"""
        )
